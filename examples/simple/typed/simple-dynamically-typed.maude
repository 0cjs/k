in ../../../k-prelude
in simple-typed-syntax
in simple-typed-programs

mod SIMPLE-DYNAMICALLY-TYPED-SEMANTICS is including SIMPLE-TYPED-DESUGARED-SYNTAX + K + K-CONTEXTS .
  sorts Val List{Val} .
  subsort Int Bool < Val < Exp .
  subsort Bottom < Val List{Bottom} < List{Val} < List{Exp} KResult .
  op _,_ : List{Val} List{Val} -> List{Val} [ditto] .
  subsorts Exp Decl Stmt Stmts List{Exp} List{IdType} List{Type} < K .

  ops env genv store fstack in out nextLoc return : -> CellLabel .

  mb configuration < T > 
                     < k > K </ k >
                     < fstack > .List </ fstack >
                     < env > .Map </ env >
                     < genv > .Map </ genv >
                     < store > .Map </ store >
                     < in > .List </ in >
                     < out > .List </ out >
                     < return > .K </ return >
                     < nextLoc > 0 </ nextLoc >
                   </ T > : KSentence .

  mb rule < k > [X => V] ...</ k > < env >... X |-> L : ? ...</ env > < store >... L |-> V ...</ store > : KSentence .
  mb rule < k > [++ X => (I +Int 1)] ...</ k > < env >... X |-> L : int ...</ env > < store >... L |-> [I => I +Int 1] ...</ store > : KSentence .
  mb rule [I1 + I2 => I1 +Int I2] : KSentence .
  mb rule [_-_(I1,I2) => _-Int_(I1,I2)] : KSentence .
  mb rule [I1 * I2 => I1 *Int I2] : KSentence .
  mb rule [I1 / I2 => I1 /Int I2] if I2 =/=Bool 0 : KSentence .
  mb rule [I1 % I2 => I1 %Int I2] if I2 =/=Bool 0 : KSentence .
  mb rule [- I => -Int I] : KSentence .
  mb rule [I1 < I2 => I1 <Int I2] : KSentence .
  mb rule [I1 <= I2 => I1 <=Int I2] : KSentence .
  mb rule [I1 > I2 => I1 >Int I2] : KSentence .
  mb rule [I1 >= I2 => I1 >=Int I2] : KSentence .
  mb rule [V1 == V2 => V1 ==Bool V2] : KSentence .
  mb rule [V1 != V2 => V1 =/=Bool V2] : KSentence .
  mb rule [T1 and T2 => T1 andBool T2] : KSentence .
  mb rule [T1 or T2 => T1 orBool T2] : KSentence .
  mb rule [not(T) => notBool(T)] : KSentence .

  mb rule < k > [array(?,L,M)[N] => Sigma(L +Int N)] ...</ k > < store > Sigma </ store > 
       if N >=Nat 0 andBool N <=Nat M : KSentence .
  mb rule < k > [read() => I] ...</ k > < in > [ListItem(I) => .List] ...</ in > : KSentence .

  op lambda : Type List{Type} List{Id} Stmt -> Val .
  op (_,_) : Map K -> ListItem .
  mb rule < k > [_`(_`)(lambda(Tp,Tpl,Xl,S),Vl) ~> K => S ~> return(initVal(Tp));] </ k > < fstack > [.List => (Env,K)] ...</ fstack >
          < env > [Env =>  GEnv[| Xl | from N / getList{K}(Xl)]] </ env > < genv > GEnv </ genv >
          < store >... [.Map => | Xl | from N |-> getList{K}(Vl)] ...</ store > < return > Tp </ return >
          < nextLoc > [N => N +Nat | Xl |] </ nextLoc > 
       if typeOf(Vl) ==Bool Tpl : KSentence .

  mb context var X[[]] : Tp[] ; : KSentence .

  mb rule [sizeOf(array(?,?,N)) => N] : KSentence .

  mb rule < k > [var X : Tp ; => .K] ...</ k > < env > [Env => Env[L : Tp / X]] </ env > < store >... [.Map => L |-> initVal(Tp)] ...</ store >
          < nextLoc > [L => sNat(L)] </ nextLoc > : KSentence .

  op array : Type Nat Nat -> Val .
  mb rule < k > [var X[N] : Tp[] ; => .K] ...</ k > < env > [Env => Env[L / X]] </ env >
          < store >...[.Map => L |-> array(Tp, sNat L, N)]...</ store > < nextLoc > [L => sNat L +Nat N] </ nextLoc > : KSentence .

  mb rule [{} => .K] : KSentence .
  mb rule < k > [{Ss} => Ss ~> env(Env)] ...</ k > < env > Env </ env > : KSentence .
  mb rule [S1 S2 => S1 ~> S2] : KSentence .
  mb rule < k > [write(I) ; => .K] ...</ k > < out >... [.List => ListItem(I)] </ out >  : KSentence .

  mb context ?[[]] = ? ; : KSentence .
  mb context [][?] = ? ; : KSentence .

  mb rule < k > [X = V ; => .K] ...</ k > < env >... X |-> L ...</ env > < store >... L |-> [? => V] ...</ store > : KSentence .
  mb rule < k > [array(Tp,L,M)[N] = V ; => .K] ...</ k > < store > [Sigma => Sigma[V / (L +Int N)]] </ store > 
       if N >=Nat 0 andBool N <=Nat M andBool typeOf(V) ==Bool Tp : KSentence .

  mb rule [if true then S else ? => S] : KSentence .
  mb rule [if false then ? else S => S] : KSentence .
  mb rule < k > [while B do S  => if B then {S while B do S} else {}] ...</ k > : KSentence .
  mb rule [V ; => .K] : KSentence .
  mb rule < k > [return(V); ~> ? => V ~> K] </ k > < fstack > [(Env,K) => .List] ...</ fstack > < env > [? => Env] </ env > : KSentence .
  mb rule < k > [.K => main();] </ k > < env > Env </ env > < genv > [.Map => Env] </ genv > : KSentence .

--- auxilliary declarations and operations
  vars I I1 I2 : Int .  var N N1 N2 M L : Nat .  var T T1 T2 : Bool .  var F X : Id .  var Env GEnv Sigma : Map .  var B : Exp .
  var S S1 S2 : Stmt .  var Xl : List{Id} .  var K : K .  var Ss : Stmts .  var Vl : List{Val} .  var V V1 V2 : Val .
  var Tp : Type .  var Tpl : List{Type} .

  op _from_ : Nat Nat -> List{K} .  eq 0 from N1 = .List{K} .  eq sNat N2 from N1 = N1,,(N2 from sNat N1) .
  op |_| : List{Bottom} -> Nat .  eq | .List{Bottom} | = 0 .  eq | X,Xl | = sNat(| Xl |) .

  op env : Map -> K .
  mb rule [env(?) ~> env(Env) => env(Env)] : KSentence [metadata "structural"] .
  mb rule < k > [env(Env) => .K] ...</ k > < env > [? => Env] </ env > : KSentence [metadata "structural"] .
  mb rule < k > V ~> [env(Env) => .K] ...</ k > < env > [? => Env] </ env > : KSentence [metadata "structural"] .

  op initVal : Type -> Val .
  eq initVal(int) = 0 .
  eq initVal(bool) = false .
  eq initVal(void) = none .
  op none : -> Val .

  op typeOf : List{Val} -> List{Type} .
  eq typeOf(I) = int .
  eq typeOf(T) = bool .
  eq typeOf(none) = void .
  eq typeOf(V1,V2,Vl) = typeOf(V1),typeOf(V2,Vl) .
endm


mod SIMPLE-DYNAMICALLY-TYPED is 
  including SIMPLE-DYNAMICALLY-TYPED-SEMANTICS .
  including SIMPLE-TYPED-PROGRAMS .
  op run : KLabel -> Bag .
  var PgmName : KLabel .  var Input : List{K} .
--- after loading simple-untyped-compiled into maude, type `rew run("pFactorial") .', etc.
  eq run(PgmName) = < T > 
             < k > PgmName(.List{K}) </ k >
             < fstack > .List </ fstack >
             < env > .Map </ env >
             < genv > .Map </ genv >
             < store > .Map </ store >
             < in > List(7,,23,,654,,656,,54,,456,,436345647643567,,354654364356,,4357643576) </ in >
             < out > .List </ out >
             < nextLoc > 0 </ nextLoc >
           </ T > .
endm

